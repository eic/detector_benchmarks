import os
import shutil

from snakemake.remote.S3 import RemoteProvider as S3RemoteProvider


S3 = S3RemoteProvider(
    endpoint_url="https://eics3.sdcc.bnl.gov:9000",
    access_key_id=os.environ["S3_ACCESS_KEY"],
    secret_access_key=os.environ["S3_SECRET_KEY"],
)


rule backgrounds_get_beam_gas_electron:
    input:
        S3.remote("eictest/EPIC/EVGEN/BACKGROUNDS/BEAMGAS/electron/beam_gas_ep_10GeV_foam_emin10keV_10Mevt_vtx.hepmc"),
    output:
        "input/backgrounds/beam_gas_electron.hepmc",
    run:
        shutil.move(input[0], output[0])


rule backgrounds_get_beam_gas_proton:
    input:
        S3.remote("eictest/EPIC/EVGEN/BACKGROUNDS/BEAMGAS/proton/ProtonBeamGasEvents/100GeV/100GeV_1.hepmc"),
    output:
        "input/backgrounds/beam_gas_proton.hepmc",
    run:
        shutil.move(input[0], output[0])


rule backgrounds_get_DIS:
    input:
        S3.remote("eictest/EPIC/EVGEN/DIS/NC/{BEAM}/minQ2={MINQ2}/pythia8NCDIS_{BEAM}_minQ2={MINQ2}_{SUFFIX}.hepmc"),
    wildcard_constraints:
        BEAM="\d+x\d+",
        MINQ2="\d+",
    output:
        "input/backgrounds/pythia8NCDIS_{BEAM}_minQ2={MINQ2}_{SUFFIX}.hepmc",
    run:
        shutil.move(input[0], output[0])


rule backgrounds_sim:
    input:
        "input/backgrounds/{NAME}.hepmc",
    output:
        "sim_output/{DETECTOR_CONFIG}/{NAME}.edm4hep.root",
    log:
        "sim_output/{DETECTOR_CONFIG}/{NAME}.edm4hep.root.log",
    params:
        N_EVENTS=100
    shell:
        """
ddsim \
  --runType batch \
  --part.minimalKineticEnergy 100*GeV  \
  --filter.tracker edep0 \
  -v WARNING \
  --numberOfEvents {params.N_EVENTS} \
  --compactFile $DETECTOR_PATH/{wildcards.DETECTOR_CONFIG}.xml \
  --inputFiles {input} \
  --outputFile {output}
"""


rule backgrounds_org2py:
    input:
        notebook=workflow.source_path("ecal_backwards.org"),
        converter=workflow.source_path("./org2py.awk"),
    output:
        "ecal_backwards.py"
    shell:
        """
awk -f {input.converter} {input.notebook} > {output}
"""

DETECTOR_CONFIG=os.environ["DETECTOR_CONFIG"]

rule backgrounds_ecal_backwards:
    input:
        matplotlibrc=".matplotlibrc",
        script="ecal_backwards.py",
        electron_beam_gas_gen="input/backgrounds/beam_gas_electron.hepmc",
        electron_beam_gas_sim="sim_output/" + DETECTOR_CONFIG + "/beam_gas_electron.edm4hep.root",
        physics_signal_sim="sim_output/" + DETECTOR_CONFIG + "/pythia8NCDIS_10x100_minQ2=1_beamEffects_xAngle=-0.025_hiDiv_1.edm4hep.root",
        proton_beam_gas_gen="input/backgrounds/beam_gas_proton.hepmc",
        proton_beam_gas_sim="sim_output/" + DETECTOR_CONFIG + "/beam_gas_proton.edm4hep.root",
    output:
        directory("results/backgrounds/backwards_ecal")
    threads: workflow.cores
    shell:
        """
PORT=$RANDOM
dask scheduler --port $PORT &
export DASK_SCHEDULER=localhost:$PORT
SCHEDULER_PID=$!
dask worker tcp://$DASK_SCHEDULER --nworkers {threads} --nthreads 1 &
WORKER_PID=$!
env \
MATPLOTLIBRC={input.matplotlibrc} \
ELECTRON_BEAM_GAS_GEN=$(realpath {input.electron_beam_gas_gen}) \
ELECTRON_BEAM_GAS_SIM=$(realpath {input.electron_beam_gas_sim}) \
PHYSICS_PROCESS_SIM=$(realpath {input.physics_signal_sim}) \
PROTON_BEAM_GAS_GEN=$(realpath {input.proton_beam_gas_gen}) \
PROTON_BEAM_GAS_SIM=$(realpath {input.proton_beam_gas_sim}) \
OUTPUT_DIR={output} \
python {input.script}
kill $WORKER_PID $SCHEDULER_PID
"""
