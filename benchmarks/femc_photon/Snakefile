def get_n_events(wildcards):
    energy = float(wildcards.P)
    n_events = 1000
    n_events = int(n_events // ((energy / 20) ** 0.5))
    return n_events


rule femc_photon_generate:
    input:
        script="benchmarks/femc_photon/analysis/gen_particles.cxx",
    output:
        GEN_FILE="sim_output/femc_photon/photon_{P}GeV.hepmc",
    params:
        N_EVENTS=get_n_events,
        th_max=28,
        th_min=2.0,
        P=lambda wildcards: wildcards.P,
    shell:
        """
mkdir -p sim_output/femc_photon
root -l -b -q '{input.script}({params.N_EVENTS},"{output.GEN_FILE}", "gamma", {params.th_min}, {params.th_max}, 0., 360., {params.P})'
"""

rule femc_photon_simulate:
    input:
        GEN_FILE="sim_output/femc_photon/photon_{P}GeV.hepmc",
        warmup="warmup/{DETECTOR_CONFIG}.edm4hep.root",
        geometry_lib=find_epic_libraries(),
    output:
        SIM_FILE="sim_output/femc_photon/{DETECTOR_CONFIG}_sim_photon_{P}GeV.edm4hep.root",
    params:
        N_EVENTS=get_n_events,
        PHYSICS_LIST="FTFP_BERT",
        DETECTOR_PATH=os.environ["DETECTOR_PATH"],
        DETECTOR_CONFIG=lambda wildcards: wildcards.DETECTOR_CONFIG,
        DD4HEP_HASH=get_spack_package_hash("dd4hep"),
        NPSIM_HASH=get_spack_package_hash("npsim"),
    cache: True
    shell:
        """
# Running simulation
npsim \
   --compactFile {params.DETECTOR_PATH}/{params.DETECTOR_CONFIG}.xml \
   --numberOfEvents {params.N_EVENTS} \
   --physicsList {params.PHYSICS_LIST} \
   --inputFiles {input.GEN_FILE} \
   --outputFile {output.SIM_FILE}
"""

rule femc_photon_recon:
    input:
        SIM_FILE="sim_output/femc_photon/{DETECTOR_CONFIG}_sim_photon_{P}GeV.edm4hep.root"
    output:
        REC_FILE="sim_output/femc_photon/{DETECTOR_CONFIG}_rec_photon_{P}GeV.edm4eic.root"
    params:
        N_EVENTS=get_n_events,
        DETECTOR_CONFIG=lambda wildcards: wildcards.DETECTOR_CONFIG,
        EICRECON_HASH=get_spack_package_hash("eicrecon"),
    cache: True
    shell:
        """
env DETECTOR_CONFIG={params.DETECTOR_CONFIG} \
  eicrecon {input.SIM_FILE} -Ppodio:output_file={output.REC_FILE} -Ppodio:output_collections=MCParticles,EcalEndcapPClusters -Pjana:nevents={params.N_EVENTS}
"""

rule femc_photon_analysis:
    input:
        expand("sim_output/femc_photon/{DETECTOR_CONFIG}_rec_photon_{P}GeV.edm4eic.root",
            P=[10, 20, 30, 40, 50, 60, 70, 80],
            DETECTOR_CONFIG=["{DETECTOR_CONFIG}"]),
        script="benchmarks/femc_photon/analysis/femc_photon_plots.py",
    output:
        results_dir=directory("results/{DETECTOR_CONFIG}/femc_photon"),
    shell:
        """
mkdir -p {output.results_dir}
python {input.script} {output.results_dir}
"""
