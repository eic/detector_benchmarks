SIMOUTDIR="sim_output/lowq2_reconstruction/"
ANALYSISDIR=SIMOUTDIR+"analysis/"

# Common list of plugins to ignore so eicrecon runs faster
LOWQ2_IGNORE_PLUGINS="janatop,LUMISPECCAL,ECTOF,BTOF,FOFFMTRK,RPOTS,B0TRK,MPGD,ECTRK,DRICH,DIRC,pid,tracking,EEMC,BEMC,FEMC,EHCAL,BHCAL,FHCAL,B0ECAL,ZDC,BTRK,BVTX,PFRICH,richgeo,evaluator,pid_lut,reco,rootfile"

##########################################################################################
### Rules for checking the reconstruction of the electron momentum
##########################################################################################

# Filter LowQ2 events from xrootd server - Using the acceptance events didn't work
# ToDo: Do a proper investigation into how the feature to target mapping isn't unique
rule filter_hepmc_for_training:
    input:
        warmup="warmup/epic_ip6_extended.edm4hep.root",
        script=workflow.source_path("filterHEPMC3.py"),        
    output:
        SIMOUTDIR+"Low-Q2_Training_Events_{BEAMENERGY}.hepmc3.tree.root",
    params:
        events="root://dtn-eic.jlab.org//volatile/eic/EPIC/EVGEN/SIDIS/pythia6-eic/1.0.0/{BEAMENERGY}/q2_0to1/pythia_ep_noradcor_{BEAMENERGY}_q2_0.000000001_1.0_run1.ab.hepmc3.tree.root",
    shell:
        """
        python {input.script} --outFile {output} --inFile {params.events}
        """

# Run pythia6 Low-Q2 events through the simulation
rule lowq2_training_sim:
    input:
        warmup="warmup/epic_ip6_extended.edm4hep.root",  
        events=SIMOUTDIR+"Low-Q2_Training_Events_{BEAMENERGY}.hepmc3.tree.root",      
        xml=lambda wildcards: workflow.source_path(f"epic_ip6_extended_{wildcards.BEAMENERGY}.xml"),
    output:
        SIMOUTDIR+"Low-Q2_Training_SimEvents_{BEAMENERGY}_{CAMPAIGN}.edm4hep.root",
    shell:
        """
            exec npsim \
            --runType run \
            --random.seed 1 \
            --inputFiles {input.events} \
            --numberOfEvents 1000000 \
            --compactFile {input.xml} \
            --printLevel WARNING \
            --outputFile {output} \
            --physics.rangecut 100*m 
        """

# Run EICrecon to create TaggerTrackerReconstructedParticles
rule lowq2_reconstruction_particles_eicrecon:
    params:
        output_collections="TaggerTrackerReconstructedParticles,MCParticles,EventHeader",
        ignore_plugins=LOWQ2_IGNORE_PLUGINS,
        ebeam=lambda wildcards: wildcards.BEAMENERGY.split('x')[0]
    input:
        data=SIMOUTDIR+"Low-Q2_Training_SimEvents_{BEAMENERGY}_{CAMPAIGN}.edm4hep.root",
        xml=lambda wildcards: workflow.source_path(f"epic_ip6_extended_{wildcards.BEAMENERGY}.xml")
    output:
        eicreconfile=ANALYSISDIR+"Low-Q2_test_Particles_{BEAMENERGY}_{CAMPAIGN}.eicrecon.edm4hep.root"
    shell:
        """
        eicrecon -Ppodio:output_file={output.eicreconfile} \
            -PLOWQ2:TaggerTrackerTransportationPreML:beamE={params.ebeam} \
            -PLOWQ2:TaggerTrackerTransportationPostML:beamE={params.ebeam} \
            -Pdd4hep:xml_files={input.xml} \
            -Pjana:nevents=500000 \
            -Ppodio:output_collections={params.output_collections} \
            -Pplugins_to_ignore={params.ignore_plugins} \
            {input.data}
        """


##########################################################################################
### Rules for training new onnx reconstruction neural network
##########################################################################################

# Run EICrecon and create input tensors for reconstruction.
rule lowq2_reconstruction_tensors_eicrecon:
    params:
        output_collections="BackwardsBeamlineHits,TaggerTrackerFeatureTensor,TaggerTrackerTargetTensor,MCParticles,EventHeader",
        ignore_plugins=LOWQ2_IGNORE_PLUGINS,
        ebeam=lambda wildcards: wildcards.BEAMENERGY.split('x')[0]
    input:
        data=SIMOUTDIR+"Low-Q2_Training_SimEvents_{BEAMENERGY}_{CAMPAIGN}.edm4hep.root",
        xml=lambda wildcards: workflow.source_path(f"epic_ip6_extended_{wildcards.BEAMENERGY}.xml")
    output:
        eicreconfile=ANALYSISDIR+"Low-Q2_Training_Tensors_{BEAMENERGY}_{CAMPAIGN}.eicrecon.edm4hep.root"
    shell:
        """
        eicrecon -Ppodio:output_file={output.eicreconfile} \
            -PLOWQ2:TaggerTrackerTransportationPreML:beamE={params.ebeam} \
            -Pdd4hep:xml_files={input.xml} \
            -Pjana:nevents=500000 -Pjana:nskip=500000 \
            -Ppodio:output_collections={params.output_collections} \
            -Pplugins_to_ignore={params.ignore_plugins} \
            {input.data}
        """

# Processes the simulation output data for training
rule lowq2_steering_reconstruction_preparation:
    params:
        ebeam=lambda wildcards: wildcards.BEAMENERGY.split('x')[0]
    input:
        script=workflow.source_path("cleanData.C"),
        data=ANALYSISDIR+"Low-Q2_Training_Tensors_{BEAMENERGY}_{CAMPAIGN}.eicrecon.edm4hep.root"
    output:
        rootfile=ANALYSISDIR+"Low-Q2_Steering_Reconstruction_Data_{BEAMENERGY}_{CAMPAIGN}.root"
    shell:
        """
        root -l -b -q '{input.script}("{input.data}", "{output.rootfile}",{params.ebeam})'
        """

# Trains a regression model to predict the MCParticle momentum from the lowq2 tagger tracker hits.
rule lowq2_steering_reconstruction_training:
    input:
        script=workflow.source_path("SteeringRegression.py"),
        scriptdata=workflow.source_path("LoadData.py"),
        scriptmodel=workflow.source_path("RegressionModel.py"),
        data=ANALYSISDIR+"Low-Q2_Steering_Reconstruction_Data_{BEAMENERGY}_{CAMPAIGN}.root",
    output:
        onnxfile=ANALYSISDIR+"Low-Q2_Steering_Reconstruction_Test_{BEAMENERGY}_{CAMPAIGN}.onnx",
    shell:
        """
        python {input.script} --dataFiles {input.data} --outModelFile {output.onnxfile} --epochs 1000
        """

# Run eicrecon with the newly trained neural network
rule lowq2_steering_reconstruction_particles_trained_eicrecon:
    params:
        output_collections="TaggerTrackerReconstructedParticles,MCParticles,EventHeader",
        ignore_plugins=LOWQ2_IGNORE_PLUGINS,
        ebeam=lambda wildcards: wildcards.BEAMENERGY.split('x')[0]
    input:
        data=SIMOUTDIR+"Low-Q2_Training_SimEvents_{BEAMENERGY}_{CAMPAIGN}.edm4hep.root",
        onnxfile=ANALYSISDIR+"Low-Q2_Steering_Reconstruction_Test_{BEAMENERGY}_{CAMPAIGN}.onnx",
        xml=lambda wildcards: workflow.source_path(f"epic_ip6_extended_{wildcards.BEAMENERGY}.xml")
    output:
        eicreconfile=ANALYSISDIR+"Low-Q2_retrained_Particles_{BEAMENERGY}_{CAMPAIGN}.eicrecon.edm4hep.root"
    shell:
        """
        eicrecon -Ppodio:output_file={output.eicreconfile} \
            -Pjana:nevents=500000 \
            -PLOWQ2:TaggerTrackerTransportationPreML:beamE={params.ebeam} \
            -PLOWQ2:TaggerTrackerTransportationPostML:beamE={params.ebeam} \
            -Pdd4hep:xml_files={input.xml} \
            -PLOWQ2:TaggerTrackerTransportationInference:modelPath={input.onnxfile} \
            -Ppodio:output_collections={params.output_collections} \
            -Pplugins_to_ignore={params.ignore_plugins} \
            {input.data}
        """

##########################################################################################
# Test and check resolutions
##########################################################################################

# Produce hitsograms of th resolution of TaggerTrackerReconstructedParticles compared to their MCParticles
rule lowq2_reconstruction_particles_test:
    params:
        ebeam=lambda wildcards: wildcards.BEAMENERGY.split('x')[0],
        pbeam=lambda wildcards: wildcards.BEAMENERGY.split('x')[1]
    input:
        script=workflow.source_path("reconstructionAnalysis.C"),
        etpscript=workflow.source_path("format_Energy_Theta_Phi_Resolutions.C"),
        eq2script=workflow.source_path("format_Energy_Q2_Acceptance.C"),
        q2recoscript=workflow.source_path("format_Q2_Reconstruction.C"),
        data=ANALYSISDIR+"Low-Q2_{STAGE}_Particles_{BEAMENERGY}_{CAMPAIGN}.eicrecon.edm4hep.root"
    output:
        rootfile=ANALYSISDIR+"Low-Q2_{STAGE}_Resolution_Results_{BEAMENERGY}_{CAMPAIGN}.root",
        momentumCanvas=ANALYSISDIR+"{STAGE}_momentum_resolution_{BEAMENERGY}_{CAMPAIGN}.png",
        energyThetaPhiCanvas=ANALYSISDIR+"{STAGE}_energy_theta_phi_resolution_{BEAMENERGY}_{CAMPAIGN}.png",
        relationCanvas=ANALYSISDIR+"{STAGE}_relation_resolution_{BEAMENERGY}_{CAMPAIGN}.png",
        resolutionGraphsCanvas=ANALYSISDIR+"{STAGE}_resolution_graphs_{BEAMENERGY}_{CAMPAIGN}.png",
        acceptanceCanvas=ANALYSISDIR+"{STAGE}_acceptance_{BEAMENERGY}_{CAMPAIGN}.png",
        energyQ2acceptanceCanvas=ANALYSISDIR+"{STAGE}_energy_Q2_acceptance_{BEAMENERGY}_{CAMPAIGN}.png",
        xQ2acceptanceCanvas=ANALYSISDIR+"{STAGE}_x_Q2_acceptance_{BEAMENERGY}_{CAMPAIGN}.png",
        Q2reconstructionCanvas=ANALYSISDIR+"{STAGE}_Q2_Reconstruction_{BEAMENERGY}_{CAMPAIGN}.png",
    shell:
        """
        root -l -b -q '{input.script}("{input.data}", {params.ebeam}, {params.pbeam}, "{output.rootfile}", "{output.momentumCanvas}", "{output.relationCanvas}", "{output.resolutionGraphsCanvas}", "{output.acceptanceCanvas}", "{output.energyQ2acceptanceCanvas}", "{output.xQ2acceptanceCanvas}")'
        root -l -b -q '{input.etpscript}("{output.rootfile}", "{output.energyThetaPhiCanvas}")'
        root -l -b -q '{input.eq2script}("{output.rootfile}", "{output.energyQ2acceptanceCanvas}")'
        root -l -b -q '{input.q2recoscript}("{output.rootfile}", "{output.Q2reconstructionCanvas}")'
        """
rule lowq2_reconstruction_x_Q2_plots:
    input:
        script=workflow.source_path("format_X_Q2_Acceptance.C"),
        data5=ANALYSISDIR+"Low-Q2_test_Resolution_Results_5x41_{CAMPAIGN}.root",
        data10=ANALYSISDIR+"Low-Q2_test_Resolution_Results_10x100_{CAMPAIGN}.root",
        data18=ANALYSISDIR+"Low-Q2_test_Resolution_Results_18x275_{CAMPAIGN}.root"
    output:
        xQ2Canvas=ANALYSISDIR+"test_X_Q2_Acceptance_{CAMPAIGN}.png"
    shell:
        """
        root -l -b -q '{input.script}("{input.data5}", "{input.data10}", "{input.data18}", "{output.xQ2Canvas}")'
        """

# Check the resloutions and offsets are within tollerance
rule lowq2_reconstruction_particles_check:
    input:
        script=workflow.source_path("checkResolutions.C"),
        rootfile=ANALYSISDIR+"Low-Q2_{STAGE}_Resolution_Results_{BEAMENERGY}_{CAMPAIGN}.root"
    output:
        jsonfile=ANALYSISDIR+"Low-Q2_{STAGE}_Resolution_Results_{BEAMENERGY}_{CAMPAIGN}.json"
    shell:
        """
        root -l -b -q '{input.script}("{input.rootfile}", "{output.jsonfile}")'
        """


##########################################################################################
# Combine results
##########################################################################################
def get_onnx_input(wildcards):
    if wildcards.STAGE == "retrained":
        return ANALYSISDIR + f"Low-Q2_Steering_Reconstruction_Test_{wildcards.BEAMENERGY}_{wildcards.CAMPAIGN}.onnx"
    else:
        return []

rule lowq2_reconstruction:
    input: 
        rootfile=ANALYSISDIR+"Low-Q2_{STAGE}_Resolution_Results_{BEAMENERGY}_{CAMPAIGN}.root",
        momentumCanvas=ANALYSISDIR+"{STAGE}_momentum_resolution_{BEAMENERGY}_{CAMPAIGN}.png",
        energyThetaPhiCanvas=ANALYSISDIR+"{STAGE}_energy_theta_phi_resolution_{BEAMENERGY}_{CAMPAIGN}.png",
        relationCanvas=ANALYSISDIR+"{STAGE}_relation_resolution_{BEAMENERGY}_{CAMPAIGN}.png",
        resolutionGraphsCanvas=ANALYSISDIR+"{STAGE}_resolution_graphs_{BEAMENERGY}_{CAMPAIGN}.png",
        acceptanceCanvas=ANALYSISDIR+"{STAGE}_acceptance_{BEAMENERGY}_{CAMPAIGN}.png",
        energyQ2acceptanceCanvas=ANALYSISDIR+"{STAGE}_energy_Q2_acceptance_{BEAMENERGY}_{CAMPAIGN}.png",
        xQ2acceptanceCanvas=ANALYSISDIR+"{STAGE}_x_Q2_acceptance_{BEAMENERGY}_{CAMPAIGN}.png",   
        jsonfile=ANALYSISDIR+"Low-Q2_{STAGE}_Resolution_Results_{BEAMENERGY}_{CAMPAIGN}.json",
        onnxfile=get_onnx_input,
    output:
        directory("results/lowq2_reconstruction/{STAGE}_{BEAMENERGY}_{CAMPAIGN}/")
    shell:
        """
            mkdir {output}        
            cp -r {input} {output}
        """

##########################################################################################
# Defualt running
##########################################################################################
rule lowq2_reconstruction_local:
    input:
        "results/lowq2_reconstruction/test_18x275_local/"

rule lowq2_reconstruction_retrained:
    input:
        "results/lowq2_reconstruction/retrained_18x275_local/"
