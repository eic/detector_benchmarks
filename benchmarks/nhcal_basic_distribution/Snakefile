import glob
from pathlib import Path

def get_energy_resolution_files(wildcards):
    pattern = f"sim_output/nhcal_basic_distribution/energy_resolution_E*GeV_{wildcards.DETECTOR_CONFIG}_*files.root"
    return sorted(glob.glob(pattern))

rule nhcal_basic_distribution_simulate:
    input:
         warmup=ancient("warmup/{DETECTOR_CONFIG}.edm4hep.root"),
    output:
        "sim_output/nhcal_basic_distribution/E{ENERGY}GeV/sim_{DETECTOR_CONFIG}.{INDEX}.edm4hep.root",
    params:
        N_EVENTS=10000,
        SEED=lambda wildcards: "1" + wildcards.INDEX,
        DETECTOR_PATH=os.environ["DETECTOR_PATH"],
        DETECTOR_CONFIG=lambda wildcards: wildcards.DETECTOR_CONFIG,
        ENERGY=lambda wildcards: wildcards.ENERGY,
        DD4HEP_HASH=get_spack_package_hash("dd4hep"),
        NPSIM_HASH=get_spack_package_hash("npsim"),
    cache: True
    shell:
        """
            exec npsim \
                --compactFile {params.DETECTOR_PATH}/{params.DETECTOR_CONFIG}.xml \
                --numberOfEvents {params.N_EVENTS} \
                --random.seed {params.SEED} \
                --enableGun \
                -v WARNING \
                --gun.particle neutron \
                --gun.thetaMin 120*degree \
                --gun.thetaMax 180*degree \
                --gun.distribution uniform \
                --gun.energy "{params.ENERGY}*GeV" \
                --outputFile {output}
        """


rule nhcal_basic_distribution_combine:
    input:
        lambda wildcards: expand(
            "sim_output/nhcal_basic_distribution/E{ENERGY:.1f}GeV/sim_{DETECTOR_CONFIG}.{INDEX}.edm4hep.root", 
            DETECTOR_CONFIG=wildcards.DETECTOR_CONFIG,
            ENERGY=float(wildcards.ENERGY),
            INDEX=range(int(wildcards.N)),
        ),
    wildcard_constraints:
        N=r"\d+",
        ENERGY=r"\d+(\.\d+)?"
    output:
        temp("sim_output/nhcal_basic_distribution/sim_E{ENERGY}GeV_{DETECTOR_CONFIG}_{N}files.edm4hep.root"),
    shell:
        """
            hadd -f {output} {input} 
        """


rule nhcal_basic_distribution_analysis:
    input:
        combined="sim_output/nhcal_basic_distribution/sim_E{ENERGY}GeV_{DETECTOR_CONFIG}_{N}files.edm4hep.root",
        script="benchmarks/nhcal_basic_distribution/scripts/basic_distribution_analysis.cxx",
    output:
        png="results/nhcal_basic_distribution/analysis_E{ENERGY}GeV_{DETECTOR_CONFIG}_{N}files.png",
        root="sim_output/nhcal_basic_distribution/energy_resolution_E{ENERGY}GeV_{DETECTOR_CONFIG}_{N}files.root",
    shell:
        """
            root -l -b -q '{input.script}("{input.combined}","{output.png}","{output.root}")'
        """
    
rule nhcal_basic_distribution_combine_energy_resolution:
    input:
        get_energy_resolution_files
    output:
        temp("sim_output/nhcal_basic_distribution/energy_resolution_combined_{DETECTOR_CONFIG}.root"),
    shell:
        """
            hadd -f {output} {input} 
        """

rule nhcal_basic_distribution_analysis_energy_resolution:
    input:
        root="sim_output/nhcal_basic_distribution/energy_resolution_combined_{DETECTOR_CONFIG}.root",
        script="benchmarks/nhcal_basic_distribution/scripts/basic_distribution_energy_resolution.cxx",
    output:
        png="results/nhcal_basic_distribution/energy_resolution_analysis_{DETECTOR_CONFIG}.png",
    shell:
        """
            root -l -b -q '{input.script}("{input.root}","{output.png}")'
        """

