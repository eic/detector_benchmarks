def get_total_energy(kinetic_energy):
    ke = float(kinetic_energy)
    neutron_mass = 0.94
    return f"{ke + neutron_mass:.2f}"

rule nhcal_basic_distribution_simulate:
    input:
        warmup="warmup.edm4hep.root",
        geometry_lib=find_epic_libraries(),
    output:
        "sim_output/nhcal_basic_distribution/E{ENERGY}GeV/sim_{DETECTOR_CONFIG}.{INDEX}.edm4hep.root",
    params:
        N_EVENTS=10000,
        SEED=lambda wildcards: "1" + wildcards.INDEX,
        DETECTOR_PATH=os.environ["DETECTOR_PATH"],
        DETECTOR_CONFIG=lambda wildcards: wildcards.DETECTOR_CONFIG,
        ENERGY=lambda wildcards: wildcards.ENERGY,
        DD4HEP_HASH=get_spack_package_hash("dd4hep"),
        NPSIM_HASH=get_spack_package_hash("npsim"),
        TOTAL_ENERGY=lambda wildcards: get_total_energy(wildcards.ENERGY),
    cache: True
    shell:
        """
exec npsim \
    --compactFile {params.DETECTOR_PATH}/{params.DETECTOR_CONFIG}.xml \
    --numberOfEvents {params.N_EVENTS} \
    --random.seed {params.SEED} \
    --enableGun \
    -v WARNING \
    --gun.particle neutron \
    --gun.thetaMin 120*degree \
    --gun.thetaMax 180*degree \
    --gun.distribution uniform \
    --gun.energy "{params.TOTAL_ENERGY}*GeV" \
    --outputFile {output}
"""


rule nhcal_basic_distribution_combine:
    input:
        lambda wildcards: expand(
            "sim_output/nhcal_basic_distribution/E{ENERGY:.1f}GeV/sim_{DETECTOR_CONFIG}.{INDEX}.edm4hep.root", 
            DETECTOR_CONFIG=wildcards.DETECTOR_CONFIG,
            ENERGY=float(wildcards.ENERGY),
            INDEX=range(int(wildcards.N)),
        ),
    wildcard_constraints:
        N=r"\d+",
        ENERGY=r"\d+(\.\d+)?"
    output:
        temp("sim_output/nhcal_basic_distribution/sim_E{ENERGY}GeV_{DETECTOR_CONFIG}_{N}files.edm4hep.root"),
    shell:
        """
            hadd -f {output} {input} 
        """

rule nhcal_basic_distribution_analysis:
    input:
        combined="sim_output/nhcal_basic_distribution/sim_E{ENERGY}GeV_{DETECTOR_CONFIG}_{N}files.edm4hep.root",
        script="benchmarks/nhcal_basic_distribution/scripts/basic_distribution_analysis.cxx",
    output:
        png="results/nhcal_basic_distribution/analysis_E{ENERGY}GeV_{DETECTOR_CONFIG}_{N}files.png",
        root="sim_output/nhcal_basic_distribution/energy_resolution_E{ENERGY}GeV_{DETECTOR_CONFIG}_{N}files.root",
    wildcard_constraints:
        ENERGY=r"\d+(\.\d+)?",
        N=r"\d+",
    params:
        DETECTOR_PATH   = os.environ["DETECTOR_PATH"],
        DETECTOR_CONFIG = lambda wildcards: f"{wildcards.DETECTOR_CONFIG}.xml", 
    shell:
        """
            root -l -b -q '{input.script}("{input.combined}","{output.png}","{output.root}","{params.DETECTOR_PATH}/{params.DETECTOR_CONFIG}")'
        """
    
rule nhcal_basic_distribution_combine_energy_resolution:
    input:
        expand(
            "sim_output/nhcal_basic_distribution/energy_resolution_E{ENERGY}GeV_{{DETECTOR_CONFIG}}_{N}files.root",
            ENERGY=["0.5", "0.7", "1.0", "2.0", "5.0"],
            N=["10"]    
        )
    output:
        temp("sim_output/nhcal_basic_distribution/energy_resolution_combined_{DETECTOR_CONFIG}.root"),
    shell:
        """
            hadd -f {output} {input} 
        """

rule nhcal_basic_distribution_analysis_energy_resolution:
    input:
        root="sim_output/nhcal_basic_distribution/energy_resolution_combined_{DETECTOR_CONFIG}.root",
        script="benchmarks/nhcal_basic_distribution/scripts/basic_distribution_energy_resolution.cxx",
    output:
        png="results/nhcal_basic_distribution/energy_resolution_analysis_{DETECTOR_CONFIG}.png",
    shell:
        """
            root -l -b -q '{input.script}("{input.root}","{output.png}")'
        """

