rule nhcal_dimuon_photoproduction_presimulate:
    output:
        "sim_input/DiMuon_ep_18x275GeV.{INDEX}.hepmc3",
    params:
        outdir=lambda wc, output: os.path.dirname(output[0]),
    shell:
        """
            benchmarks/nhcal_dimuon_photoproduction/scripts/run.sh {wildcards.INDEX} {params.outdir} 
        """

rule nhcal_dimuon_photoproduction_simulate:
    input:
        "sim_input/DiMuon_ep_18x275GeV.{INDEX}.hepmc3",
    output:
        "sim_output/nhcal_dimuon_photoproduction/sim_{DETECTOR_CONFIG}.{INDEX}.edm4hep.root",
    params:
        N_EVENTS=1000,
        SEED=lambda wildcards: "1" + wildcards.INDEX,
        DETECTOR_PATH=os.environ["DETECTOR_PATH"],
        DETECTOR_CONFIG=lambda wildcards: wildcards.DETECTOR_CONFIG,
        DD4HEP_HASH=get_spack_package_hash("dd4hep"),
        NPSIM_HASH=get_spack_package_hash("npsim"),
    wildcard_constraints:
        DETECTOR_CONFIG = r"[A-Za-z0-9_]+",
        INDEX = r"\d+",
    cache: True
    shell:
        """
            exec npsim \
                --compactFile {params.DETECTOR_PATH}/{params.DETECTOR_CONFIG}.xml \
                --inputFile {input} \
                -v WARNING \
                --random.seed {params.SEED} \
                --numberOfEvents {params.N_EVENTS} \
                --outputFile {output}
        """

rule nhcal_dimuon_photoproduction_recon:
    input:
        "sim_output/nhcal_dimuon_photoproduction/sim_{DETECTOR_CONFIG}.{INDEX}.edm4hep.root"
    output:
        "sim_output/nhcal_dimuon_photoproduction/sim_{DETECTOR_CONFIG}.{INDEX}.eicrecon.edm4hep.root"
    params:
        DETECTOR_CONFIG = lambda wildcards: wildcards.DETECTOR_CONFIG,
    cache: True
    shell: 
        """
            exec env DETECTOR_CONFIG={params.DETECTOR_CONFIG} \
                eicrecon {input} \
                -Ppodio:output_file={output} \
                
        """

rule nhcal_dimuon_photoproduction_combine:
    input:
        lambda wildcards: expand(
            "sim_output/nhcal_dimuon_photoproduction/sim_{DETECTOR_CONFIG}.{INDEX}.eicrecon.edm4hep.root",
            DETECTOR_CONFIG=wildcards.DETECTOR_CONFIG,
            INDEX=range(int(wildcards.N)),
        )
    output:
        "sim_output/nhcal_dimuon_photoproduction/sim_{DETECTOR_CONFIG}_{N}files.eicrecon.edm4hep.root",
    wildcard_constraints:
        N = r"\d+",
    shell:
        """
            hadd -f {output} {input} 
        """

rule nhcal_dimuon_photoproduction_analysis:
    input:
        combined="sim_output/nhcal_dimuon_photoproduction/sim_{DETECTOR_CONFIG}_{N}files.eicrecon.edm4hep.root",
        script="benchmarks/nhcal_dimuon_photoproduction/scripts/dimuon_photoproduction_analysis.cxx",
    output:
        png=f"results/nhcal_dimuon_photoproduction/analysis_{{DETECTOR_CONFIG}}_{{N}}files.png",
        pdf=f"results/nhcal_dimuon_photoproduction/analysis_{{DETECTOR_CONFIG}}_{{N}}files.pdf",
    params:
        DETECTOR_PATH   = os.environ["DETECTOR_PATH"],
        DETECTOR_CONFIG = lambda wildcards: f"{wildcards.DETECTOR_CONFIG}.xml",
    shell:
        """
            root -l -b -q '{input.script}("{input.combined}","{output.pdf}","{output.png}","{params.DETECTOR_PATH}/{params.DETECTOR_CONFIG}")'
        """
