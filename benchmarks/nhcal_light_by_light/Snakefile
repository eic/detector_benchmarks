rule nhcal_light_by_light_simulate:
    input:
        "benchmarks/nhcal_light_by_light/DiMuon_ep_18x275GeV.hepmc3",
    output:
        "sim_output/nhcal_light_by_light/sim_{DETECTOR_CONFIG}.{INDEX}.edm4hep.root",
    params:
        N_EVENTS=200,
        #SEED=lambda wildcards: "1" + wildcards.INDEX,
        DETECTOR_PATH=os.environ["DETECTOR_PATH"],
        DETECTOR_CONFIG=lambda wildcards: wildcards.DETECTOR_CONFIG,
        #ENERGY=lambda wildcards: wildcards.ENERGY,
        #DD4HEP_HASH=get_spack_package_hash("dd4hep"),
        #NPSIM_HASH=get_spack_package_hash("npsim"),
    wildcard_constraints:
        DETECTOR_CONFIG = r"[A-Za-z0-9_]+",
        INDEX            = r"\d+",
    #cache: True
    shell:
        """
exec npsim \
    --compactFile {params.DETECTOR_PATH}/{params.DETECTOR_CONFIG}.xml \
    --inputFile {input} \
    -v WARNING \
    --numberOfEvents {params.N_EVENTS} \
    --outputFile {output}
"""

rule nhcal_light_by_light_recon:
    input:
        "sim_output/nhcal_light_by_light/sim_{DETECTOR_CONFIG}.{INDEX}.edm4hep.root"
    output:
        "sim_output/nhcal_light_by_light/sim_{DETECTOR_CONFIG}.{INDEX}.eicrecon.edm4hep.root"
    params:
        DETECTOR_CONFIG = lambda wildcards: wildcards.DETECTOR_CONFIG,
    #cache: True
    shell: 
        """
exec env DETECTOR_CONFIG={params.DETECTOR_CONFIG} \
          eicrecon {input} \
          -Ppodio:output_file={output} \
          -Ppodio:output_collections=MCParticles,InclusiveKinematicsTruth
"""

rule nhcal_light_by_light_combine_reco:
    input:
        lambda wildcards: expand(
            "sim_output/nhcal_light_by_light/sim_{DETECTOR_CONFIG}.{INDEX}.eicrecon.edm4hep.root",
            DETECTOR_CONFIG=wildcards.DETECTOR_CONFIG,
            INDEX=range(int(wildcards.N)),
        )
    output:
        "sim_output/nhcal_light_by_light/sim_{DETECTOR_CONFIG}_{N}files.eicrecon.edm4hep.root",
    wildcard_constraints:
        N = r"\d+",
    shell:
        """
hadd -f {output} {input} 
"""

rule nhcal_light_by_light_analysis:
    input:
        combined="sim_output/nhcal_light_by_light/sim_{DETECTOR_CONFIG}_{N}files.eicrecon.edm4hep.root",
        script="benchmarks/nhcal_light_by_light/scripts/light_by_light_analysis.cxx",
    output:
        png=f"results/nhcal_light_by_light/analysis_{{DETECTOR_CONFIG}}_{{N}}files.png",
        pdf=f"results/nhcal_light_by_light/analysis_{{DETECTOR_CONFIG}}_{{N}}files.pdf",
    shell:
        """
    root -l -b -q '{input.script}("{input.combined}","{output.pdf}","{output.png}")'
"""