rule nhcal_sampling_fraction_simulate:
    output:
        "sim_output/nhcal_sampling_fraction/{PARTICLE}/E{ENERGY}GeV/sim.edm4hep.root",
    params:
        N_EVENTS=5000,
        DETECTOR_PATH=os.environ["DETECTOR_PATH"],
        DETECTOR_CONFIG="epic_backward_hcal_only_sampF.xml",
        DD4HEP_HASH=get_spack_package_hash("dd4hep"),
        NPSIM_HASH=get_spack_package_hash("npsim"),
        TOTAL_ENERGY=lambda wildcards: {
            "neutron": {"0.5": "1.44", "0.7": "1.64", "1.0": "1.94", "2.0": "2.94", "5.0": "5.94", "10.0": "10.94"},
            "pi-": {"0.5": "0.64", "0.7": "0.84", "1.0": "1.14", "2.0": "2.14", "5.0": "5.14", "10.0": "10.14"},
            "e-": {"0.5": "0.50", "0.7": "0.70", "1.0": "1.00", "2.0": "2.00", "5.0": "5.00", "10.0": "10.00"}
        }[wildcards.PARTICLE][wildcards.ENERGY],
    cache: True
    shell:
        """
set -m
exec npsim \
    --compactFile {params.DETECTOR_PATH}/{params.DETECTOR_CONFIG} \
    --numberOfEvents {params.N_EVENTS} \
    --random.seed $RANDOM \
    --enableGun \
    -v WARNING \
    --gun.particle {wildcards.PARTICLE} \
    --gun.thetaMin 120*degree \
    --gun.thetaMax 180*degree \
    --gun.distribution uniform \
    --gun.energy "{params.TOTAL_ENERGY}*GeV" \
    --outputFile {output}
"""


rule nhcal_sampling_fraction_combine:
    input:
        lambda wildcards: expand(
            "sim_output/nhcal_sampling_fraction/{PARTICLE}/E{ENERGY}GeV/sim.edm4hep.root", 
            ENERGY=["0.5", "0.7", "1.0", "2.0", "5.0", "10.0"],
            PARTICLE=["pi-", "neutron", "e-"],
        ),
    wildcard_constraints:
        ENERGY=r"\d+"
    output:
        f"sim_output/nhcal_sampling_fraction/sim_combined.edm4hep.root",
    shell:
        """
hadd -f {output} {input} 
"""

rule nhcal_sampling_fraction_analysis:
    input:
        combined="sim_output/nhcal_sampling_fraction/sim_combined.edm4hep.root",
        script="benchmarks/nhcal_sampling_fraction/scripts/sampling_fraction_analysis.cxx",
    output:
        pdf="results/nhcal_sampling_fraction/hist_sampf_vs_Ehit.pdf",
        png="results/nhcal_sampling_fraction/hist_sampf_vs_Ehit.png",
    params:
        DETECTOR_PATH=os.environ["DETECTOR_PATH"],
        DETECTOR_CONFIG="epic_backward_hcal_only_sampF.xml"
    shell:
        """
    root -l -b -q '{input.script}("{input.combined}","{output.pdf}","{output.png}","{params.DETECTOR_PATH}/{params.DETECTOR_CONFIG}")'
"""
