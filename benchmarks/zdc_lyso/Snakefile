import os


rule zdc_lyso_sim_hepmc:
    input:
        script = "benchmarks/zdc_lyso/gen_particles.cxx",
    output:
        hepmcfile="data/{PARTICLE}_{BEAM_ENERGY}GeV_theta_{THETA_MIN}deg_thru_{THETA_MAX}deg.hepmc",
    log:
        "data/{PARTICLE}_{BEAM_ENERGY}GeV_theta_{THETA_MIN}deg_thru_{THETA_MAX}deg.hepmc.log",
    params:
        num_events=1000,
        PARTICLE=lambda wildcards: wildcards.PARTICLE,
        BEAM_ENERGY=lambda wildcards: wildcards.BEAM_ENERGY,
        THETA_MIN=lambda wildcards: wildcards.THETA_MIN,
        THETA_MAX=lambda wildcards: wildcards.THETA_MAX,
    shell:
        """
root -l -b -q '{input.script}({params.num_events}, "{output.hepmcfile}", "{params.PARTICLE}", {params.THETA_MIN}, {params.THETA_MAX}, 0, 360, {params.BEAM_ENERGY})'
"""


rule zdc_lyso_sim:
    input:
        hepmcfile="data/{PARTICLE}_{BEAM_ENERGY}GeV_theta_{THETA_MIN}deg_thru_{THETA_MAX}deg.hepmc",
        warmup=ancient("warmup/{DETECTOR_CONFIG}.edm4hep.root"),
        geometry_lib=find_epic_libraries(),
    output:
        "sim_output/zdc_lyso/{DETECTOR_CONFIG}_{PARTICLE}_{BEAM_ENERGY}GeV_theta_{THETA_MIN}deg_thru_{THETA_MAX}deg.edm4hep.root",
    log:
        "sim_output/zdc_lyso/{DETECTOR_CONFIG}_{PARTICLE}_{BEAM_ENERGY}GeV_theta_{THETA_MIN}deg_thru_{THETA_MAX}deg.edm4hep.root.log",
    params:
        num_events=1000,
        DETECTOR_PATH=os.environ["DETECTOR_PATH"],
        DETECTOR_CONFIG=lambda wildcards: wildcards.DETECTOR_CONFIG,
        DD4HEP_HASH=get_spack_package_hash("dd4hep"),
        NPSIM_HASH=get_spack_package_hash("npsim"),
    cache: True
    shell:
        """
exec npsim \
  --runType batch \
  -v WARNING \
  --compactFile {params.DETECTOR_PATH}/{params.DETECTOR_CONFIG}.xml \
  --random.seed 1 \
  --numberOfEvents {params.num_events} \
  --inputFiles {input.hepmcfile} \
  --outputFile {output}
"""


rule zdc_lyso_reco:
    input:
        "sim_output/zdc_lyso/{DETECTOR_CONFIG}_{PARTICLE}_{BEAM_ENERGY}GeV_theta_{THETA_MIN}deg_thru_{THETA_MAX}deg.edm4hep.root",
        warmup=ancient("warmup/{DETECTOR_CONFIG}.edm4hep.root"),
    output:
        "sim_output/zdc_lyso/{DETECTOR_CONFIG}_{PARTICLE}_{BEAM_ENERGY}GeV_theta_{THETA_MIN}deg_thru_{THETA_MAX}deg.eicrecon.edm4eic.root",
    log:
        "sim_output/zdc_lyso/{DETECTOR_CONFIG}_{PARTICLE}_{BEAM_ENERGY}GeV_theta_{THETA_MIN}deg_thru_{THETA_MAX}deg.eicrecon.edm4eic.root.log",
    params:
        DETECTOR_CONFIG=lambda wildcards: wildcards.DETECTOR_CONFIG,
        EICRECON_HASH=get_spack_package_hash("eicrecon"),
    cache: True
    shell:
        """
env DETECTOR_CONFIG={params.DETECTOR_CONFIG} \
  eicrecon -Ppodio:output_file={output} -Ppodio:output_collections=HcalFarForwardZDCRawHits,HcalFarForwardZDCRecHits,HcalFarForwardZDCClusters,EcalFarForwardZDCRawHits,EcalFarForwardZDCRecHits,EcalFarForwardZDCClusters,MCParticles {input}
"""


rule zdc_lyso_analysis:
    input:
        expand("sim_output/zdc_lyso/{{DETECTOR_CONFIG}}_{PARTICLE}_{BEAM_ENERGY}GeV_theta_{THETA_MIN}deg_thru_{THETA_MAX}deg.eicrecon.edm4eic.root",
               PARTICLE=["gamma"],
               BEAM_ENERGY=["0.005", "0.01", "0.05", "0.1", "0.5", "1.0"],
               THETA_MIN=["0"],
               THETA_MAX=["0.3"]),
        script="benchmarks/zdc_lyso/analysis/analysis.py",
    output:
        "results/{DETECTOR_CONFIG}/zdc_lyso/plots.pdf",
    shell:
        """
python {input.script}
"""


# Examples of invocation
rule zdc_lyso_hepmc:
    input:
        expand("data/{PARTICLE}_{BEAM_ENERGY}GeV_theta_{THETA_MIN}deg_thru_{THETA_MAX}deg.hepmc",
               PARTICLE=["gamma"],
               BEAM_ENERGY=["0.005", "0.01", "0.05", "0.1", "0.5", "1.0"],
               THETA_MIN=["0"],
               THETA_MAX=["0.3"])


rule zdc_lyso_local:
   input:
        "results/" + os.environ["DETECTOR_CONFIG"] + "/zdc_lyso/plots.pdf",
   message:
        "See output in {input[0]}"

