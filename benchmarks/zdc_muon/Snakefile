rule zdc_muon_generate:
	input:
                script="benchmarks/zdc_muon/analysis/gen_particles.cxx",
	params:
		NEVENTS_GEN=1000,
		th_max=0.23,
		th_min=0
	output:
		GEN_FILE="sim_output/zdc_muon/mu-_{P}GeV.hepmc"
	shell:
		"""
root -l -b -q '{input.script}({params.NEVENTS_GEN},"{output.GEN_FILE}", "mu-", {params.th_min}, {params.th_max}, 0., 360., {wildcards.P})'
"""

rule zdc_muon_simulate:
	input:
		GEN_FILE="sim_output/zdc_muon/mu-_{P}GeV.hepmc"
	params:
		PHYSICS_LIST="FTFP_BERT"
	output:
		SIM_FILE="sim_output/zdc_muon/{DETECTOR_CONFIG}_sim_mu-_{P}GeV.edm4hep.root"
	shell:
		"""
NEVENTS_SIM=1000
# Running simulation
npsim \
   --compactFile $DETECTOR_PATH/{wildcards.DETECTOR_CONFIG}.xml \
   --numberOfEvents $NEVENTS_SIM \
   --physicsList {params.PHYSICS_LIST} \
   --inputFiles {input.GEN_FILE} \
   --outputFile {output.SIM_FILE}
"""

rule zdc_muon_recon:
        input:
                SIM_FILE="sim_output/zdc_muon/{DETECTOR_CONFIG}_sim_mu-_{P}GeV.edm4hep.root"
        output:
                REC_FILE="sim_output/zdc_muon/{DETECTOR_CONFIG}_rec_mu-_{P}GeV.edm4hep.root"
        shell:
                """
NEVENTS_REC=1000
eicrecon {input.SIM_FILE} -Ppodio:output_file={output.REC_FILE} -Pdd4hep:xml_files=$DETECTOR_PATH/{wildcards.DETECTOR_CONFIG}.xml -Ppodio:output_collections=MCParticles,HcalFarForwardZDCRecHits,HcalFarForwardZDCClusters,HcalFarForwardZDCSubcellHits, -Pjana:nevents=$NEVENTS_REC
"""

rule zdc_muon_analysis:
	input:
                expand("sim_output/zdc_muon/{DETECTOR_CONFIG}_rec_mu-_{P}GeV.edm4hep.root",
		    P=[50],
		    DETECTOR_CONFIG=["{DETECTOR_CONFIG}"]),
                script="benchmarks/zdc_muon/analysis/muon_plots.py",
	output:
		results_dir=directory("results/{DETECTOR_CONFIG}/zdc_muon"),
	shell:
		"""
mkdir -p {output.results_dir}
python {input.script} {output.results_dir}
"""
