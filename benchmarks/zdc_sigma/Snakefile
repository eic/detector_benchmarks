rule zdc_sigma_generate:
    input:
        script="benchmarks/zdc_sigma/analysis/gen_sigma_decay.cxx",
    output:
        GEN_FILE="sim_output/zdc_sigma/sigma_decay_{P}GeV.hepmc",
    params:
        N_EVENTS=100000,
        P=lambda wildcards: wildcards.P,
    shell:
        """
root -l -b -q '{input.script}({params.N_EVENTS},0,"{output.GEN_FILE}",{params.P},{params.P})'
"""

rule zdc_sigma_simulate:
    input:
        GEN_FILE="sim_output/zdc_sigma/sigma_decay_{P}GeV.hepmc",
        warmup=ancient("warmup/{DETECTOR_CONFIG}.edm4hep.root"),
        geometry_lib=find_epic_libraries(),
    output:
        SIM_FILE="sim_output/zdc_sigma/{DETECTOR_CONFIG}_sim_sigma_dec_{P}GeV_{INDEX}.edm4hep.root",
    params:
        N_EVENTS=200,
        INDEX=lambda wildcards: wildcards.INDEX,
        SEED=lambda wildcards: "1" + wildcards.INDEX,
        PHYSICS_LIST="FTFP_BERT",
        DETECTOR_PATH=os.environ["DETECTOR_PATH"],
        DETECTOR_CONFIG=lambda wildcards: wildcards.DETECTOR_CONFIG,
        DD4HEP_HASH=get_spack_package_hash("dd4hep"),
        NPSIM_HASH=get_spack_package_hash("npsim"),
    cache: True
    shell:
        """
exec npsim \
   --compactFile {params.DETECTOR_PATH}/{params.DETECTOR_CONFIG}.xml \
   --random.seed {params.SEED} \
   --skipNEvents $(( {params.N_EVENTS} * {params.INDEX} )) \
   --numberOfEvents {params.N_EVENTS} \
   --physicsList {params.PHYSICS_LIST} \
   --inputFiles {input.GEN_FILE} \
   --outputFile {output.SIM_FILE}
"""

rule zdc_sigma_recon:
    input:
        SIM_FILE="sim_output/zdc_sigma/{DETECTOR_CONFIG}_sim_sigma_dec_{P}GeV_{INDEX}.edm4hep.root",
        warmup=ancient("warmup/{DETECTOR_CONFIG}.edm4hep.root"),
    output:
        REC_FILE="sim_output/zdc_sigma/{DETECTOR_CONFIG}_rec_sigma_dec_{P}GeV_{INDEX}.edm4eic.root",
    params:
        N_EVENTS=200,
        DETECTOR_CONFIG=lambda wildcards: wildcards.DETECTOR_CONFIG,
        EICRECON_HASH=get_spack_package_hash("eicrecon"),
    cache: True
    shell:
        """
env DETECTOR_CONFIG={params.DETECTOR_CONFIG} \
  eicrecon {input.SIM_FILE} -Ppodio:output_file={output.REC_FILE} -Ppodio:output_collections=MCParticles,HcalFarForwardZDCClusters,HcalFarForwardZDCRecHits,HcalFarForwardZDCSubcellHits -Pjana:nevents={params.N_EVENTS}
"""

rule zdc_sigma_analysis:
    input:
        expand("sim_output/zdc_sigma/{DETECTOR_CONFIG}_rec_sigma_dec_{P}GeV_{INDEX}.edm4eic.root",
            P=[100, 125, 150,175, 200, 225, 250, 275],
            DETECTOR_CONFIG=["{DETECTOR_CONFIG}"],
            INDEX=range(5),
        ),
        script="benchmarks/zdc_sigma/analysis/sigma_plots.py",
    output:
        results_dir=directory("results/{DETECTOR_CONFIG}/zdc_sigma"),
    shell:
        """
mkdir -p {output.results_dir}
python {input.script} {output.results_dir}
"""
